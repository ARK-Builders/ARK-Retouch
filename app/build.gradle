import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.21'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "dev.arkbuilders.arkretouch2"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"
        setProperty("archivesBaseName", "ark-retouch")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        testRelease {
            storeFile project.rootProject.file('./keystore.jks')
            storePassword "sw0rdf1sh"
            keyAlias "ark-builders-test"
            keyPassword "rybamech"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                appIcon: "@mipmap/ic_launcher_1",
                appLabel: "@string/app_name_debug"
            ]
        }
        release {
            signingConfig signingConfigs.testRelease
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                appIcon: "@mipmap/ic_launcher_2",
                appLabel: "@string/app_name"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.14"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'dev.arkbuilders.arkretouch'
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2024.11.00')
    implementation composeBom
    testImplementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0"

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation "com.github.bumptech.glide:glide:4.16.0"
    kapt "com.github.bumptech.glide:compiler:4.16.0"

    implementation 'com.google.dagger:dagger:2.50'
    kapt 'com.google.dagger:dagger-compiler:2.50'

    implementation 'com.godaddy.android.colorpicker:compose-color-picker:0.7.0'

    implementation 'dev.arkbuilders:arkfilepicker:0.1.1'
    implementation "androidx.navigation:navigation-compose:2.5.2"

    implementation 'io.github.hokofly:hoko-blur:1.5.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}

task installGitHook(type: Copy) {
    def suffix = "linux"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        suffix = "windows"
    }
    from new File(rootProject.rootDir, "scripts/pre-commit-$suffix")
    into { new File(rootProject.rootDir, '.git/hooks') }
    rename("pre-commit-$suffix", 'pre-commit')
    fileMode 0775
}

tasks.preBuild.dependsOn installGitHook
tasks.preBuild.dependsOn ktlintCheck
